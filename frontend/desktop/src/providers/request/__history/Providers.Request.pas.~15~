unit Providers.Request;

interface

uses
  System.SysUtils, System.Classes, System.JSON, RestRequest4D, Providers.Request.Intf;

type
  IRequest = Providers.Request.Intf.IRequest;
  IResponse = RestRequest4D.IResponse;
  TRequest = class(TInterfacedObject, IRequest)
  private
    FRequest: RestRequest4D.IRequest;
    function BaseURL(const AValue: string): IRequest;
    function Resource(const AValue: string): IRequest;
    function ResourceSuffix(const AValue: string): IRequest;
    function AddParam(const AKey, AValue: string): IRequest;
    function ContentType(const AValue: string): IRequest;
    function ClearBody: IRequest;
    function ClearParams: IRequest;
    function AddBody(const ABody: TJSONObject; const AOwns: Boolean = True): IRequest; overload;
    function AddBody(const ABody: TMemoryStream; const AOwns: Boolean = True): IRequest; overload;
    function Get: IResponse;
    function Post: IResponse;
    function Delete: IResponse;
    function Put: IResponse;
    procedure DoBeforeExecute;
    constructor Create;
  public
    class function New: IRequest;
    destructor Destroy; override;
  end;

implementation

{ TRequest }

uses Providers.Session;

function TRequest.AddBody(const ABody: TMemoryStream;
  const AOwns: Boolean): IRequest;
begin
  FRequest.AddBody(ABody, AOwns);
  Result := Self;
end;

function TRequest.AddBody(const ABody: TJSONObject;
  const AOwns: Boolean): IRequest;
begin
  FRequest.AddBody(ABody, AOwns);
  Result := Self;
end;

function TRequest.AddParam(const AKey, AValue: string): IRequest;
begin
  FRequest.AddParam(AKey, AValue);
  Result := Self;
end;

function TRequest.BaseURL(const AValue: string): IRequest;
begin
  FRequest.BaseURL(AValue);
  Result := Self;
end;

function TRequest.ClearBody: IRequest;
begin
  FRequest.ClearBody;
  Result := Self;
end;

function TRequest.ClearParams: IRequest;
begin
  FRequest.ClearParams;
  Result := Self;
end;

function TRequest.ContentType(const AValue: string): IRequest;
begin
  FRequest.ContentType(AValue);
  Result := Self;
end;

constructor TRequest.Create;
begin
  FRequest := RestRequest4D.TRequest.New;
end;

function TRequest.Delete: IResponse;
begin
  DoBeforeExecute;
  Result := FRequest.Delete;
end;

destructor TRequest.Destroy;
begin
  FRequest := nil;
  inherited;
end;

procedure TRequest.DoBeforeExecute;
begin
  if not TSession.GetInstance.Token.Access.Trim.IsEmpty then
    FRequest.Token('bearer ' + TSession.GetInstance.Token.Access);
end;

function TRequest.Get: IResponse;
begin
  DoBeforeExecute;
  Result := FRequest.Get;
end;

class function TRequest.New: IRequest;
begin
  Result := TRequest.Create;
end;

function TRequest.Post: IResponse;
begin
  DoBeforeExecute;
  Result := FRequest.Post;
end;

function TRequest.Put: IResponse;
begin
  DoBeforeExecute;
  Result := FRequest.Put;
end;

function TRequest.Resource(const AValue: string): IRequest;
begin
  FRequest.Resource(AValue);
  Result := Self;
end;

function TRequest.ResourceSuffix(const AValue: string): IRequest;
begin
  FRequest.ResourceSuffix(AValue);
  Result := Self;
end;

end.
